module analysis/types

imports
  signatures/c18-sig
  
  analysis/util
  analysis/declarations

//signature // to also consider qualifiers 
// sorts TYPE TSPEC TQUAL constructors
// INT : TSPEC
// CONST : TQUAL
// TYPE : list(TQUAL) * TSPEC -> TYPE

signature
  sorts TYPE constructors
  FUN           : list(TYPE) * TYPE -> TYPE
  VARFUN        : list(TYPE) * TYPE -> TYPE

  VOID 					: TYPE
  CHAR 					: TYPE
  SCHAR 				: TYPE
  UCHAR 				: TYPE
  SHORT 				: TYPE
  USHORT 				: TYPE
  INT 					: TYPE
  UINT 					: TYPE
  LONG 					: TYPE
  ULONG 				: TYPE
  LONGLONG 			: TYPE
  ULONGLONG 		: TYPE
  FLOAT 				: TYPE
  DOUBLE 				: TYPE
  LONGDOUBLE 		: TYPE
  BOOL 					: TYPE
  COMPLEX 			: TYPE -> TYPE
  IMAGINARY			: TYPE -> TYPE
  STRUCT        : scope -> TYPE
  UNION					: scope -> TYPE
  STRUCT_REF    : ID -> TYPE
  UNION_REF     : ID -> TYPE
  ENUM					: TYPE

  // TODO type qualifiers
  // ATOMIC
  
  POINTER : TYPE -> TYPE
  
  // base type * list of dimensions
  // int[2][4] -> ARRAY(INT, [2, 4])
  ARRAY : TYPE * list(int) -> TYPE

rules // typing  rules
  typeOfType : scope * list(TypeSpecifier) -> TYPE

  // void
  typeOfType(_, [Void()]) = VOID().

  // char
  typeOfType(_, [Char()]) = CHAR().

  // signed char
  typeOfType(_, [Signed(), Char()]) = SCHAR().

  // unsigned char
  typeOfType(_, [Unsigned(), Char()]) = UCHAR().

  // short
  typeOfType(_, [Short()]) = SHORT().
  typeOfType(_, [Signed(), Short()]) = SHORT().
  typeOfType(_, [Short(), Int()]) = SHORT().
  typeOfType(_, [Signed(), Short(), Int()]) = SHORT().

  // unsigned short
  typeOfType(_, [Unsigned(), Short()]) = USHORT().
  typeOfType(_, [Unsigned(), Short(), Int()]) = USHORT().

  // int
  typeOfType(_, [Int()]) = INT().
  typeOfType(_, [Signed()]) = INT().
  typeOfType(_, [Signed(), Int()]) = INT().

  // unsigned int
  typeOfType(_, [Unsigned()]) = UINT().
  typeOfType(_, [Unsigned(), Int()]) = UINT().

  // long
  typeOfType(_, [Long()]) = LONG().
  typeOfType(_, [Signed(), Long()]) = LONG().
  typeOfType(_, [Long(), Int()]) = LONG().
  typeOfType(_, [Signed(), Long(), Int()]) = LONG().

  // unsigned long
  typeOfType(_, [Unsigned(), Long()]) = ULONG().
  typeOfType(_, [Unsigned(), Long(), Int()]) = ULONG().

  // long long
  typeOfType(_, [Long(), Long()]) = LONGLONG().
  typeOfType(_, [Signed(), Long(), Long()]) = LONGLONG().
  typeOfType(_, [Long(), Long(), Int()]) = LONGLONG().
  typeOfType(_, [Signed(), Long(), Long(), Int()]) = LONGLONG().

  // unsigned long long
  typeOfType(_, [Unsigned(), Long(), Long()]) = ULONGLONG().
  typeOfType(_, [Unsigned(), Long(), Long(), Int()]) = ULONGLONG().

  // float
  typeOfType(_, [Float()]) = FLOAT().

  // double
  typeOfType(_, [Double()]) = DOUBLE().

  // long double
  typeOfType(_, [Long(), Double()]) = LONGDOUBLE().

  // _Bool
  typeOfType(_, [Bool()]) = BOOL().

  // _Complex
  typeOfType(_, [t1, Complex()]) = COMPLEX(SUB) :-
	complexSubtype([t1]) == SUB.
  typeOfType(_, [t1, t2, Complex()]) = COMPLEX(SUB) :-
	complexSubtype([t1, t2]) == SUB.

  // _ Imaginary
  typeOfType(_, [t1, Imaginary()]) = IMAGINARY(SUB) :-
	complexSubtype([t1]) == SUB.
  typeOfType(_, [t1, t2, Imaginary()]) = IMAGINARY(SUB) :-
	complexSubtype([t1, t2]) == SUB.

  // TODO struct
  // TODO union
  // TODO enum
  
  typeOfType(s, [StructOrUnionSpecifier2TypeSpecifier(StructOrUnion(sou, id, decls))]) = 
    typeOfStructOrUnionDecl(s, sou, id, decls).

  typeOfType(s, [StructOrUnionSpecifier2TypeSpecifier(StructOrUnionDecl(Struct(), id))]) =
    STRUCT_REF(id).

  typeOfType(s, [StructOrUnionSpecifier2TypeSpecifier(StructOrUnionDecl(Union(), id))]) =
    UNION_REF(id).

  typeOfType(s, [EnumSpecifier2TypeSpecifier(EnumSpec(id, es))]) =
    typeOfEnum(s, id, es).

  typeOfType(s, [EnumSpecifier2TypeSpecifier(EnumSpecDecl(id))]) = ENUM() :-
    enumDeclared(s, id).

  // typedef names
  typeOfType(s, [TypedefName1(x)]) = typeOfTypedef(s, x). // TODO perform lookup for 'x'

rules // type helpers
	// Only permit float, double and long double as complex types
	complexSubtype : list(TypeSpecifier) -> TYPE
	complexSubtype([Float(), _]) = FLOAT().
	complexSubtype([Double(), _]) = DOUBLE().
	complexSubtype([Long(), Double(), _]) = LONGDOUBLE().
	
	
rules intType : TYPE

  intType(ENUM()).
  // standard signed integer types
  intType(SCHAR()).
  intType(SHORT()).
  intType(INT()).
  intType(LONG()).
  intType(LONGLONG()).
  // standard unsigned integer types
  intType(CHAR()).
  intType(BOOL()).
  intType(T) :- {ST}
    signEquiv(ST, T).

rules floatingType : TYPE
  floatingType(FLOAT()).
  floatingType(DOUBLE()).
  floatingType(LONGDOUBLE()).
  floatingType(COMPLEX(T)) :- complexSubtype(_) == T.

rules arithmeticType : TYPE
  arithmeticType(FLOAT()).
  arithmeticType(DOUBLE()).
  arithmeticType(LONGDOUBLE()).
  arithmeticType(COMPLEX(T)) :-
    complexSubtype(_) == T.
  arithmeticType(IMAGINARY(T)) :-
    complexSubtype(_) == T.
  arithmeticType(T) :-
    intType(T).

rules scalarType : TYPE

  scalarType(POINTER(_)).
  scalarType(T) :- arithmeticType(T).
	
rules signEquiv : TYPE * TYPE

  signEquiv(SCHAR(), UCHAR()).
  signEquiv(SHORT(), USHORT()).
  signEquiv(INT(), UINT()).
  signEquiv(LONG(), ULONG()).
  signEquiv(LONGLONG(), ULONGLONG()).
	
rules intConvRank : TYPE -> int

  // ยง 6.3.1.1
  // every type has an integer conversion rank, no two types shall have the same rank, the rank of
  // a signed integer shall be greater than the rank of any other with less precision,
  // long long > long > int > short > signed char
  intConvRank(SCHAR()) = 10.
  intConvRank(SHORT()) = 20.
  intConvRank(INT()) = 30.
  intConvRank(LONG()) = 40.
  intConvRank(LONGLONG()) = 50.
  
  // The rank of any unsigned integer type shall equal the rank of the corresponding signed integer
  // ... *however*: If an int can represent all values of the original type (as restricted by the 
  // width, for a bit-field), the value is converted to an int; otherwise, it is converted to an 
  // unsigned int.
  // We don't know if it is going to fit so we rank unsigned types 1 above signed types
  intConvRank(T) = R :- {ST Sr}
    signEquiv(ST, T),
    intConvRank(ST) == Sr,
    R == #(Sr + 1).
    
    
  // The rank of any standard integer type shall be greater than the rank of any extended integer
  // type with the same width, the rank of any extended signed integer type relative to another 
  // extended signed integer type with the same precision is implementation-defined, but still 
  // subject to the other rules for determining the integer conversion rank.
  // ... we don't have those I think
  
  // The rank of char shall equal the rank of signed char and unsigned char.
  intConvRank(CHAR()) = intConvRank(SCHAR()).
  
  // The rank of _Bool shall be less than the rank of all other standard integer types.
  intConvRank(BOOL()) = 5.
  
  // The rank of any enumerated type shall equal the rank of the compatible integer type 
  // (see 6.7.2.2).
  // fix it to int for now
  intConvRank(ENUM()) = intConvRank(INT()).
  
  // Should be warnings at some point when converting from ints to floats or above (as not all
  // integers are representable as floats)
  intConvRank(FLOAT()) = 100.
  intConvRank(DOUBLE()) = 200.
  
rules signed : TYPE

  signed(SCHAR()).
  signed(SHORT()).
  signed(INT()).
  signed(LONG()).
  signed(LONGLONG()).
	
rules lub : TYPE * TYPE -> TYPE
  lubs maps lub(list(*), list(*)) = list(*)

  lub(T, T) = T.
  lub(POINTER(Ta), POINTER(VOID())) = POINTER(Ta).
  lub(POINTER(VOID()), POINTER(Ta)) = POINTER(Ta).
  lub(Ta, Tb) = T :- {Ra Rb Rm}
    intConvRank(Ta) == Ra,
    intConvRank(Tb) == Rb,
    #(max(Ra, Rb)) == Rm,
    projIntEq(Rm, Ra, Ta, Tb) == T.
    
rules projIntEq : int * int * TYPE * TYPE -> TYPE

  projIntEq(i, i, T, _) = convertIntType(T).
  projIntEq(_, _, _, T) = convertIntType(T).
  
rules convertIntType : TYPE -> TYPE
  // The expression that defines the value of an enumeration constant shall be
  // an integer constant expression that has a value representable as an int.
  convertIntType(ENUM()) = INT().
  convertIntType(Ti) = To :-
    Ti == To | error $[[Ti] is not implicitly convertible to [To]].
